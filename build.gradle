plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta6'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'dev.ua.uaproject.catwalk'
version = '0.8-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "codemc-snapshots"
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
    maven {
        name = "codemc-public"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "reposilite-snapshots"
        url = "https://maven.reposilite.com/snapshots"
    }
}

dependencies {
    // Core Paper API (always included)
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly("org.apache.logging.log4j:log4j-core:2.20.0")

    // Soft dependencies (compileOnly)
    compileOnly("me.clip:placeholderapi:2.11.6")

    // Lombok (annotation processor)
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Lightweight dependencies to include in JAR
    implementation("com.google.code.gson:gson:2.13.1")
    compileOnly("org.mariadb.jdbc:mariadb-java-client:3.3.2")
    compileOnly("com.zaxxer:HikariCP:5.1.0")

    // Test dependencies (not included in JAR)
    testImplementation 'org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.46.0'
    testImplementation("com.konghq:unirest-java:3.14.5:standalone")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.3")

    // Dependencies that will be loaded by our custom loader - use compileOnly
    compileOnly("io.javalin:javalin-bundle:6.6.0")
    compileOnly("io.javalin.community.openapi:javalin-openapi-plugin:6.6.0")
    compileOnly("io.javalin.community.openapi:javalin-swagger-plugin:6.6.0")
    compileOnly("io.javalin.community.openapi:javalin-redoc-plugin:6.6.0")
    compileOnly("io.javalin.community.ssl:ssl-plugin:6.6.0")

    compileOnly('org.apache.commons:commons-compress:1.26.0')
    compileOnly('commons-io:commons-io:2.14.0')
    compileOnly("org.apache.commons:commons-lang3:3.12.0")

    compileOnly("com.fasterxml.jackson.core:jackson-core:2.16.1")
    compileOnly("com.fasterxml.jackson.core:jackson-databind:2.16.1")
    compileOnly("com.fasterxml.jackson.core:jackson-annotations:2.16.1")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    runServer {
        minecraftVersion("1.21.4")
    }
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    filteringCharset = 'UTF-8'

    filesMatching('paper-plugin.yml') {
        expand(props)
    }
}

shadowJar {
    archiveClassifier = ''

    from(sourceSets.main.resources) {
        include '**/*'
    }

    relocate 'com.google.gson', 'dev.ua.uaproject.catwalk.libs.gson'
}
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}